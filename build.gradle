plugins {
    id 'java'
    id 'distribution'
    id 'org.hidetake.ssh' version '2.10.1'
}

group 'org.example'
version '0.1'

repositories {
    mavenCentral()
}

distributions {
    main {
        version = ''
        contents {
            into('config') {
                from 'config'
            }
            into ('lib') {
                from jar
                from(configurations.runtimeClasspath)
            }

            from 'runscripts'
        }
    }
}

remotes {
    master {
        host = 'quinta.navigators.di.fc.ul.pt'
        user = 'rvassantlal'
        //password = 'root'
        identity = file(System.getProperty("user.home") + System.getProperty("file.separator")
                + ".ssh" + System.getProperty("file.separator") + "id_rsa")
    }
}

ssh.settings {
    fileTransfer = 'scp'
}

task remoteDeploy (dependsOn: installDist) {
    doLast {
        ssh.run {
            session(remotes.master) {
                put from: "build/install/${project.name}", into: '~/'
            }
        }
    }
}

task localDeploy (dependsOn: installDist) {
    doLast {
        int[] replicas = new int[]{0, 1, 2, 3}
        int[] clients = new int[]{0}
        String dst = System.getProperty("user.home") + System.getProperty("file.separator") +
                "Desktop" + System.getProperty("file.separator") +
                project.name + System.getProperty("file.separator")

        println ("Deploying project into ${dst}")

        replicas.each {
            def target = dst + System.getProperty("file.separator") + "rep${it}"
            copy {
                from "build/install/${project.name}"
                into target
            }
        }

        clients.each {
            def target = dst + System.getProperty("file.separator") + "cli${it}"
            copy {
                from "build/install/${project.name}"
                into target
            }
        }
    }
}

dependencies {
    implementation fileTree('libs') {include '*.jar'}

    // https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk15on
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.69'

    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'

    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    implementation 'commons-codec:commons-codec:1.15'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    implementation 'ch.qos.logback:logback-core:1.2.5'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation 'ch.qos.logback:logback-classic:1.2.5'

    // https://mvnrepository.com/artifact/io.netty/netty-all
    implementation 'io.netty:netty-all:4.1.67.Final'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation 'org.slf4j:slf4j-api:1.7.32'
}